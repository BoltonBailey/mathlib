/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import algebra.category.CommRing.basic
import algebra.category.Group.limits
import deprecated.subgroup

/-!
# The category of commutative rings has all limits

Further, these limits are preserved by the forgetful functor --- that is,
the underlying types are just the limits in the category of types.

## Further work
A lot of this should be generalised / automated, as it's quite common for concrete
categories that the forgetful functor preserves limits.
-/

open category_theory
open category_theory.limits

universe u

namespace CommRing

variables {J : Type u} [small_category J]

instance comm_ring_obj (F : J ‚•§ CommRing) (j) :
  comm_ring ((F ‚ãô forget CommRing).obj j) :=
by { change comm_ring (F.obj j), apply_instance }

/--
The flat sections of a functor into `CommRing` form a multiplicative submonoid of all sections.
-/
def sections_submonoid (F : J ‚•§ CommRing) :
  submonoid (Œ† j, F.obj j) :=
{ carrier := (F ‚ãô forget CommRing).sections,
  one_mem' := Œª j j' f, by simp,
  mul_mem' := Œª a b ah bh j j' f,
  begin
    simp only [forget_map_eq_coe, functor.comp_map, ring_hom.map_mul, pi.mul_apply],
    dsimp [functor.sections] at ah bh,
    rw [ah f, bh f],
  end }

-- We still don't have bundled subrings,
-- so we need to convert the bundled sub-objects back to unbundled

instance sections_submonoid' (F : J ‚•§ CommRing) :
  is_submonoid (F ‚ãô forget CommRing).sections :=
(sections_submonoid F).is_submonoid

instance sections_add_subgroup' (F : J ‚•§ CommRing) :
  is_add_subgroup (F ‚ãô forget CommRing).sections :=
(AddCommGroup.sections_add_subgroup (F ‚ãô forget‚ÇÇ CommRing Ring ‚ãô forget‚ÇÇ Ring AddCommGroup)).is_subgroup

instance sections_subring (F : J ‚•§ CommRing) :
  is_subring (F ‚ãô forget CommRing).sections := {}

instance limit_comm_ring (F : J ‚•§ CommRing) :
  comm_ring (limit (F ‚ãô forget CommRing)) :=
@subtype.comm_ring ((Œ† (j : J), (F ‚ãô forget _).obj j)) (by apply_instance) _
  (by convert (CommRing.sections_subring F))

/-- `limit.œÄ (F ‚ãô forget CommRing) j` as a `ring_hom`. -/
def limit_œÄ_ring_hom (F : J ‚•§ CommRing) (j) :
  limit (F ‚ãô forget CommRing) ‚Üí+* (F ‚ãô forget CommRing).obj j :=
{ to_fun := limit.œÄ (F ‚ãô forget CommRing) j,
  map_one' := by { simp only [types.types_limit_œÄ], refl },
  map_zero' := by { simp only [types.types_limit_œÄ], refl },
  map_mul' := Œª x y, by { simp only [types.types_limit_œÄ], refl },
  map_add' := Œª x y, by { simp only [types.types_limit_œÄ], refl } }

namespace CommRing_has_limits
-- The next two definitions are used in the construction of `has_limits CommRing`.
-- After that, the limits should be constructed using the generic limits API,
-- e.g. `limit F`, `limit.cone F`, and `limit.is_limit F`.

/--
Construction of a limit cone in `CommRing`.
(Internal use only; use the limits API.)
-/
def limit (F : J ‚•§ CommRing) : cone F :=
{ X := CommRing.of (limit (F ‚ãô forget _)),
  œÄ :=
  { app := limit_œÄ_ring_hom F,
    naturality' := Œª j j' f,
      ring_hom.coe_inj ((limit.cone (F ‚ãô forget _)).œÄ.naturality f) } }

/--
Witness that the limit cone in `CommRing` is a limit cone.
(Internal use only; use the limits API.)
-/
def limit_is_limit (F : J ‚•§ CommRing) : is_limit (limit F) :=
begin
  refine is_limit.of_faithful
    (forget CommRing) (limit.is_limit _)
    (Œª s, ‚ü®_, _, _, _, _‚ü©) (Œª s, rfl); tidy
end

end CommRing_has_limits
open CommRing_has_limits

/-- The category of commutative rings has all limits. -/
instance CommRing_has_limits : has_limits CommRing :=
{ has_limits_of_shape := Œª J ùí•,
  { has_limit := Œª F, by exactI
    { cone     := limit F,
      is_limit := limit_is_limit F } } }

/--
The forgetful functor from commutative rings to types preserves all limits. (That is, the underlying
types could have been computed instead as limits in the category of types.)
-/
instance forget_preserves_limits : preserves_limits (forget CommRing) :=
{ preserves_limits_of_shape := Œª J ùí•,
  { preserves_limit := Œª F,
    by exactI preserves_limit_of_preserves_limit_cone
      (limit.is_limit F) (limit.is_limit (F ‚ãô forget _)) } }

end CommRing
