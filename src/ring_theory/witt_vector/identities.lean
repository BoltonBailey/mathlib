/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin
-/

import ring_theory.witt_vector.frobenius
import ring_theory.witt_vector.verschiebung
import ring_theory.witt_vector.mul_p

/-!
## Identities between operations on the ring of Witt vectors

In this file we show deduce common identities between the Frobenius and Verschiebung operators.

### Main declarations

* `frobenius_verschiebung`: the composition of Frobenius and Verschiebung is multiplication by `p`
* `verschiebung_mul_frobenius`: the ‚Äúproduct formula‚Äù: `V(x * F y) = V x * y`
-/

namespace witt_vector

variables {p : ‚Ñï} {R : Type*} [hp : fact p.prime] [comm_ring R]
local notation `ùïé` := witt_vector p -- type as `\bbW`

local attribute [semireducible] witt_vector
local attribute [instance] mv_polynomial.invertible_rat_coe_nat

open mv_polynomial
noncomputable theory

include hp

/-- The composition of Frobenius and Verschiebung is multiplication by `p`. -/
lemma frobenius_verschiebung (x : ùïé R) :
  frobenius (verschiebung x) = x * p :=
begin
  ghost_calc x,
  introsI,
  simp only [ghost_component_verschiebung, ghost_component_frobenius,
    ring_hom.map_mul, ring_hom.map_nat_cast, mul_comm],
end

lemma verschiebung_zmod (x : ùïé (zmod p)) :
  verschiebung x = x * p :=
begin
  rw [‚Üê frobenius_verschiebung, frobenius_zmodp],
end

lemma coeff_p_pow [char_p R p] (i : ‚Ñï) : (p ^ i : ùïé R).coeff i = 1 :=
begin
  induction i with i h,
  { simp only [one_coeff_zero, ne.def, pow_zero] },
  { rw [pow_succ', ‚Üê frobenius_verschiebung, coeff_frobenius_char_p,
        verschiebung_coeff_succ, h, one_pow], }
end

/-- The ‚Äúproduct formula‚Äù for Frobenius and Verschiebung. -/
lemma verschiebung_mul_frobenius (x y : ùïé R) :
  verschiebung (x * frobenius y) = verschiebung x * y :=
begin
  ghost_calc x y,
  rintro ‚ü®‚ü©;
  simp [ghost_component_zero_verschiebung, zero_mul, ring_hom.map_mul,
    ghost_component_verschiebung, ghost_component_frobenius, nat.succ_eq_add_one, mul_assoc],
end

end witt_vector
